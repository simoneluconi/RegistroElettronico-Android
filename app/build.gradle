buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

android {
    compileSdkVersion 26

    defaultConfig {
        applicationId "com.sharpdroid.registroelettronico"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 527
        versionName "2.1.27"

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    dataBinding {
        enabled true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    signingConfigs {
        config {
            if (rootProject.file("keystore.properties").exists()) {
                def keystorePropertiesFile = rootProject.file("keystore.properties")
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        debug {
            debuggable true

            if (rootProject.file("keystore.properties").exists()) {
                signingConfig signingConfigs.config
            }
        }

        release {
            debuggable false

            zipAlignEnabled true
            shrinkResources false
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            if (rootProject.file("keystore.properties").exists()) {
                signingConfig signingConfigs.config
            }
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    sourceSets {
        androidTest.assets.srcDirs +=
                files("$projectDir/schemas".toString())
    }

    return void
}

allprojects {
    repositories {
        maven {
            url 'https://jitpack.io'
        }
        maven {
            url 'https://dl.bintray.com/vsvankhede/maven/'
        }
        google()
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true;
    }
    implementation 'com.google.firebase:firebase-core:16.0.8'
    implementation('com.mikepenz:materialdrawer:6.0.7@aar') {
        exclude group: 'com.android.support'
        transitive = true
    }

    implementation 'com.android.support.constraint:constraint-layout:1.1.0'

    implementation('com.afollestad.material-dialogs:core:0.9.6.0') {
        exclude group: 'com.android.support'
    }
    implementation('com.afollestad.material-dialogs:commons:0.9.6.0') {
        exclude group: 'com.android.support'
    }

    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.11.0'
    implementation 'com.akexorcist:RoundCornerProgressBar:2.0.3'
    implementation 'com.andkulikov:transitionseverywhere:1.7.6'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:preference-v14:26.1.0'
    implementation 'com.android.support:preference-v7:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.android.support:support-v13:26.1.0'
    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'com.github.clans:fab:1.6.4'
    implementation('com.github.PhilJay:MPAndroidChart:v3.0.3') {
        exclude group: 'com.android.support'
    }
    implementation 'com.github.sundeepk:compact-calendar-view:2.0.2.2'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation ('com.heinrichreimersoftware:material-intro:7221502f1f') {
        exclude group: 'com.android.support'
    }
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation('com.wdullaer:materialdatetimepicker:3.5.1') {
        exclude group: 'com.android.support'
    }
    implementation 'com.yqritc:recyclerview-flexibledivider:1.4.0'
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.12'

    //room
    implementation "android.arch.persistence.room:runtime:1.1.1"
    implementation "android.arch.persistence.room:rxjava2:1.1.1"
    kapt "android.arch.persistence.room:compiler:1.1.1"

    //LiveData & ViewModel
    implementation "android.arch.lifecycle:extensions:1.1.1"

    //For Lifecycles Java8 Lanaguage support, add:
    implementation "android.arch.lifecycle:common-java8:1.1.1"

    implementation 'com.google.firebase:firebase-messaging:17.6.0'
    implementation 'com.firebase:firebase-jobdispatcher:0.8.5'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'com.android.support:multidex:1.0.3'

    androidTestImplementation 'android.arch.persistence.room:testing:1.1.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
}

apply plugin: 'com.google.gms.google-services'
